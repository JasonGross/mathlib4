/-
Copyright (c) 2024 Thomas Browning. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Thomas Browning
-/
import Mathlib.Algebra.Order.Group.Action.Synonym
import Mathlib.FieldTheory.Fixed
import Mathlib.NumberTheory.RamificationInertia
import Mathlib.RingTheory.Ideal.Pointwise

/-!
# Frobenius Elements

In algebraic number theory, if `L/K` is a finite Galois extension of number fields, with rings of
integers `ùìûL/ùìûK`, and if `q` is prime ideal of `ùìûL` lying over a prime ideal `p` of `ùìûK`, then
there exists unique a **Frobenius element** `Frob p` in `Gal(L/K)` with the property that
`Frob p x ‚âÉ x ^ #(ùìûK/p) (mod q)` for all `x ‚àà ùìûL`.

This file proves the existence of Frobenius elements in a much more general setting.

## Main statements



## Implementation notes


-/

open scoped Pointwise

section ForMathlib

instance Ideal.IsPrime.smul {R : Type*} [CommRing R] {G : Type*} [Group G] [MulSemiringAction G R]
    {P : Ideal R} [P.IsPrime] (g : G) : (g ‚Ä¢ P).IsPrime :=
  Ideal.map_isPrime_of_equiv (MulSemiringAction.toRingEquiv _ _ g)

lemma Finset.smul_prod_perm
    {A : Type*} [CommMonoid A] {G : Type*} [Group G] [Fintype G] [MulDistribMulAction G A]
    (a : A) (g0 : G) : g0 ‚Ä¢ (‚àè g : G, g ‚Ä¢ a) = ‚àè g : G, g ‚Ä¢ a := by
  simp_rw [Finset.smul_prod', smul_smul]
  exact Finset.prod_bijective (fun g ‚Ü¶ g0 * g)
    (Group.mulLeft_bijective g0) (by simp) (fun g _ ‚Ü¶ rfl)

-- PRed
lemma le_pow_smul {G : Type*} [Monoid G] {Œ± : Type*} [Preorder Œ±] {g : G} {a : Œ±}
    [MulAction G Œ±] [CovariantClass G Œ± HSMul.hSMul LE.le]
    (h : a ‚â§ g ‚Ä¢ a) (n : ‚Ñï) : a ‚â§ g ^ n ‚Ä¢ a := by
  induction' n with n hn
  ¬∑ rw [pow_zero, one_smul]
  ¬∑ rw [pow_succ', mul_smul]
    exact h.trans (smul_mono_right g hn)

-- PRed
instance {G : Type*} [Monoid G] {Œ± : Type*} [Preorder Œ±]
    [MulAction G Œ±] [CovariantClass G Œ± HSMul.hSMul LE.le] :
    CovariantClass G Œ±·µí·µà HSMul.hSMul LE.le :=
  ‚ü®fun g _ _ h ‚Ü¶ smul_mono_right (Œ± := Œ±) g h‚ü©

-- PRed
lemma pow_smul_le {G : Type*} [Monoid G] {Œ± : Type*} [Preorder Œ±] {g : G} {a : Œ±}
    [MulAction G Œ±] [CovariantClass G Œ± HSMul.hSMul LE.le]
    (h : g ‚Ä¢ a ‚â§ a) (n : ‚Ñï) : g ^ n ‚Ä¢ a ‚â§ a :=
  le_pow_smul (Œ± := Œ±·µí·µà) h n

-- PRed
lemma smul_eq_of_le_smul
    {G : Type*} [Group G] [Finite G] {Œ± : Type*} [PartialOrder Œ±] {g : G} {a : Œ±}
    [MulAction G Œ±] [CovariantClass G Œ± HSMul.hSMul LE.le]
    (h : a ‚â§ g ‚Ä¢ a) : g ‚Ä¢ a = a := by
  have key := smul_mono_right g (le_pow_smul h (Nat.card G - 1))
  rw [smul_smul, ‚Üê pow_succ',
    Nat.sub_one_add_one_eq_of_pos Nat.card_pos, pow_card_eq_one', one_smul] at key
  exact le_antisymm key h

-- PRed
lemma smul_eq_of_smul_le
    {G : Type*} [Group G] [Finite G] {Œ± : Type*} [PartialOrder Œ±] {g : G} {a : Œ±}
    [MulAction G Œ±] [CovariantClass G Œ± HSMul.hSMul LE.le]
    (h : g ‚Ä¢ a ‚â§ a) : g ‚Ä¢ a = a :=
  smul_eq_of_le_smul (Œ± := Œ±·µí·µà) h

end ForMathlib

section part_a

lemma comap_smul {A B : Type*} [CommRing A] [CommRing B] [Algebra A B]
    {G : Type*} [Group G] [MulSemiringAction G B] [SMulCommClass G A B]
    (P : Ideal B) (g : G) : (g ‚Ä¢ P).comap (algebraMap A B) = P.comap (algebraMap A B) := by
  ext a
  rw [Ideal.mem_comap, Ideal.mem_comap, Ideal.mem_pointwise_smul_iff_inv_smul_mem,
      Algebra.algebraMap_eq_smul_one, smul_comm, smul_one]

variable {A B : Type*} [CommRing A] [CommRing B] [Algebra A B]
  {G : Type*} [Group G] [Finite G] [MulSemiringAction G B] [SMulCommClass G A B]

-- (Part a of Th√©or√®me 2 in section 2 of chapter 5 of Bourbaki Alg Comm)
theorem part_a (P Q : Ideal B) [hP : P.IsPrime] [hQ : Q.IsPrime]
    (hPQ : Ideal.comap (algebraMap A B) P = Ideal.comap (algebraMap A B) Q)
    (hAB : ‚àÄ (b : B), (‚àÄ (g : G), g ‚Ä¢ b = b) ‚Üí ‚àÉ a : A, b = algebraMap A B a) :
    ‚àÉ g : G, Q = g ‚Ä¢ P := by
  cases nonempty_fintype G
  have : ‚àÄ (P Q : Ideal B) [P.IsPrime] [Q.IsPrime],
      P.comap (algebraMap A B) = Q.comap (algebraMap A B) ‚Üí ‚àÉ g ‚àà (‚ä§ : Finset G), Q ‚â§ g ‚Ä¢ P := by
    intro P Q hP hQ hPQ
    rw [‚Üê Ideal.subset_union_prime 1 1 (fun _ _ _ _ ‚Ü¶ hP.smul _)]
    intro b hb
    suffices h : ‚àÉ g ‚àà Finset.univ, g ‚Ä¢ b ‚àà P by
      obtain ‚ü®g, -, hg‚ü© := h
      apply Set.mem_biUnion (Finset.mem_univ g‚Åª¬π) (Ideal.mem_inv_pointwise_smul_iff.mpr hg)
    obtain ‚ü®a, ha‚ü© := hAB (‚àè g : G, g ‚Ä¢ b) (Finset.smul_prod_perm b)
    rw [‚Üê hP.prod_mem_iff, ha, ‚Üê P.mem_comap, hPQ, Q.mem_comap, ‚Üê ha, hQ.prod_mem_iff]
    exact ‚ü®1, Finset.mem_univ 1, (one_smul G b).symm ‚ñ∏ hb‚ü©
  obtain ‚ü®g, -, hg‚ü© := this P Q hPQ
  obtain ‚ü®g', -, hg'‚ü© := this Q (g ‚Ä¢ P) ((comap_smul P g).trans hPQ).symm
  have hg'' := hg.trans hg'
  have key := smul_eq_of_le_smul hg''
  rw [key] at hg'
  exact ‚ü®g, le_antisymm hg hg'‚ü©

end part_a

section lifting

variable (A B : Type*)
  [CommRing A] [CommRing B]
  [IsDomain A] [IsDomain B]
  (K L : Type*) [Field K] [Field L]
  [Algebra A K] [IsFractionRing A K]
  [Algebra B L] [IsFractionRing B L]
  [Algebra A B] [Algebra K L] [Algebra A L]
  [IsScalarTower A B L] [IsScalarTower A K L]

-- PRed
@[simp] lemma IsFractionRing.fieldEquivOfRingEquiv_commutes (f : B ‚âÉ+* B) (b : B) :
    IsFractionRing.fieldEquivOfRingEquiv f (algebraMap B L b) = algebraMap B L (f b) := by
  simp only [IsFractionRing.fieldEquivOfRingEquiv, IsLocalization.ringEquivOfRingEquiv_eq]

-- PRed
noncomputable def lift (f : B ‚âÉ‚Çê[A] B) : L ‚âÉ‚Çê[K] L where
  __ := IsFractionRing.fieldEquivOfRingEquiv f.toRingEquiv
  commutes' := by
    intro x
    obtain ‚ü®x, y, -, rfl‚ü© := IsFractionRing.div_surjective (A := A) x
    simp only [AlgEquiv.toRingEquiv_eq_coe, RingEquiv.toEquiv_eq_coe, Equiv.toFun_as_coe,
      EquivLike.coe_coe]
    rw [map_div‚ÇÄ, map_div‚ÇÄ]
    rw [‚Üê IsScalarTower.algebraMap_apply, ‚Üê IsScalarTower.algebraMap_apply]
    rw [IsScalarTower.algebraMap_apply A B L, IsScalarTower.algebraMap_apply A B L]
    rw [IsFractionRing.fieldEquivOfRingEquiv_commutes]
    rw [IsFractionRing.fieldEquivOfRingEquiv_commutes]
    simp only [AlgEquiv.toRingEquiv_eq_coe, AlgEquiv.coe_ringEquiv, AlgEquiv.commutes]

-- PRed
noncomputable def liftHom : (B ‚âÉ‚Çê[A] B) ‚Üí* (L ‚âÉ‚Çê[K] L) where
  toFun := lift A B K L
  map_one' := by
    ext x
    obtain ‚ü®x, y, -, rfl‚ü© := IsFractionRing.div_surjective (A := B) x
    simp [lift, IsFractionRing.fieldEquivOfRingEquiv]
  map_mul' := fun f g ‚Ü¶ by
    ext x
    obtain ‚ü®x, y, -, rfl‚ü© := IsFractionRing.div_surjective (A := B) x
    simp [lift, IsFractionRing.fieldEquivOfRingEquiv]

-- PRed
lemma liftHom_commutes (f : B ‚âÉ‚Çê[A] B) (b : B) :
    liftHom A B K L f (algebraMap B L b) = algebraMap B L (f b) := by
  simp [liftHom, lift, IsFractionRing.fieldEquivOfRingEquiv]

end lifting

section MulSemiringAction

variable {G : Type*} (R A : Type*) [CommSemiring R] [Semiring A] [Algebra R A]
variable [Group G]

-- PRed
@[simps]
def MulSemiringAction.ofAlgEquivHom (h : G ‚Üí* A ‚âÉ‚Çê[R] A) : MulSemiringAction G A where
  smul := fun g r ‚Ü¶ h g r
  one_smul := DFunLike.ext_iff.mp (map_one h)
  mul_smul := fun g g' ‚Ü¶ DFunLike.ext_iff.mp (map_mul h g g')
  smul_zero := fun g ‚Ü¶ map_zero (h g)
  smul_add := fun g ‚Ü¶ map_add (h g)
  smul_one := fun g ‚Ü¶ map_one (h g)
  smul_mul := fun g ‚Ü¶ map_mul (h g)

variable [MulSemiringAction G A] [SMulCommClass G R A]

-- PRed
@[simps]
def MulSemiringAction.toAlgEquivHom : G ‚Üí* A ‚âÉ‚Çê[R] A where
  toFun := MulSemiringAction.toAlgEquiv R A
  map_one' := by ext; rw [toAlgEquiv_apply, one_smul]; rfl
  map_mul' := fun f g ‚Ü¶ by ext; rw [toAlgEquiv_apply, mul_smul]; rfl

end MulSemiringAction

section MulSemiringAction

variable {G : Type*} (R : Type*) [Semiring R] [Group G]

-- PRed
def MulSemiringAction.ofHom (h : G ‚Üí* R ‚âÉ+* R) : MulSemiringAction G R where
  smul := fun g r ‚Ü¶ h g r
  one_smul := DFunLike.ext_iff.mp (map_one h)
  mul_smul := fun g g' ‚Ü¶ DFunLike.ext_iff.mp (map_mul h g g')
  smul_zero := fun g ‚Ü¶ map_zero (h g)
  smul_add := fun g ‚Ü¶ map_add (h g)
  smul_one := fun g ‚Ü¶ map_one (h g)
  smul_mul := fun g ‚Ü¶ map_mul (h g)

variable [MulSemiringAction G R]

-- PRed
def MulSemiringAction.toHom : G ‚Üí* R ‚âÉ+* R  where
  toFun := MulSemiringAction.toRingEquiv G R
  map_one' := by ext; rw [toRingEquiv_apply, one_smul]; rfl
  map_mul' := fun f g ‚Ü¶ by ext; rw [toRingEquiv_apply, mul_smul]; rfl

end MulSemiringAction

section fixedfield

/-- `MulSemiringAction.toAlgHom` is bijective. -/
theorem toAlgHom_bijective' (G F : Type*) [Field F] [Group G] [Finite G] [MulSemiringAction G F]
    [FaithfulSMul G F] : Function.Bijective
      (MulSemiringAction.toAlgEquivHom _ _ : G ‚Üí* F ‚âÉ‚Çê[FixedPoints.subfield G F] F) := by
  refine ‚ü®fun _ _ h ‚Ü¶ (FixedPoints.toAlgHom_bijective G F).injective ?_,
    fun f ‚Ü¶ ((FixedPoints.toAlgHom_bijective G F).surjective f).imp (fun _ h ‚Ü¶ ?_)‚ü©
      <;> rwa [DFunLike.ext_iff] at h ‚ä¢

/-- `MulSemiringAction.toAlgHom` is surjective. -/
theorem toAlgHom_surjective (G F : Type*) [Field F] [Group G] [Finite G] [MulSemiringAction G F] :
    Function.Surjective
      (MulSemiringAction.toAlgEquivHom _ _ : G ‚Üí* F ‚âÉ‚Çê[FixedPoints.subfield G F] F) := by
  let f : G ‚Üí* F ‚âÉ‚Çê[FixedPoints.subfield G F] F :=
    MulSemiringAction.toAlgEquivHom (FixedPoints.subfield G F) F
  let Q := G ‚ß∏ f.ker
  let _ : MulSemiringAction Q F := MulSemiringAction.ofAlgEquivHom _ _ (QuotientGroup.kerLift f)
  have : FaithfulSMul Q F := ‚ü®by
    intro q‚ÇÅ q‚ÇÇ
    refine Quotient.inductionOn‚ÇÇ' q‚ÇÅ q‚ÇÇ (fun g‚ÇÅ g‚ÇÇ h ‚Ü¶ QuotientGroup.eq.mpr ?_)
    rwa [MonoidHom.mem_ker, map_mul, map_inv, inv_mul_eq_one, AlgEquiv.ext_iff]‚ü©
  intro f
  obtain ‚ü®q, hq‚ü© := (toAlgHom_bijective' Q F).surjective
    (AlgEquiv.ofRingEquiv (f := f) (fun ‚ü®x, hx‚ü© ‚Ü¶ f.commutes' ‚ü®x, fun g ‚Ü¶ hx g‚ü©))
  revert hq
  refine QuotientGroup.induction_on q (fun g hg ‚Ü¶ ‚ü®g, ?_‚ü©)
  rwa [AlgEquiv.ext_iff] at hg ‚ä¢

end fixedfield

section integrallemma

theorem Polynomial.nonzero_const_if_isIntegral (R S : Type*) [CommRing R] [Ring S] [Algebra R S]
    [h : Algebra.IsAlgebraic R S] [NoZeroDivisors S] (s : S) (hs : s ‚â† 0) :
    ‚àÉ (q : Polynomial R), q.coeff 0 ‚â† 0 ‚àß aeval s q = 0 := by
  obtain ‚ü®p, hp0, hp‚ü© := h.isAlgebraic s
  obtain ‚ü®q, hpq, hq‚ü© := Polynomial.exists_eq_pow_rootMultiplicity_mul_and_not_dvd p hp0 0
  rw [C_0, sub_zero] at hpq hq
  rw [hpq, map_mul, aeval_X_pow, mul_eq_zero, or_iff_right (pow_ne_zero _ hs)] at hp
  exact ‚ü®q, mt X_dvd_iff.mpr hq, hp‚ü©

theorem Algebra.exists_dvd_nonzero_if_isIntegral (R S : Type*) [CommRing R]
    [CommRing S] [Algebra R S] [Algebra.IsAlgebraic R S] [NoZeroDivisors S] (s : S) (hs : s ‚â† 0) :
    ‚àÉ r : R, r ‚â† 0 ‚àß s ‚à£ (algebraMap R S) r := by
  obtain ‚ü®q, hq0, hq‚ü© := Polynomial.nonzero_const_if_isIntegral R S s hs
  have key := map_dvd (Polynomial.aeval s) (Polynomial.X_dvd_sub_C (p := q))
  rw [map_sub, hq, zero_sub, dvd_neg, Polynomial.aeval_X, Polynomial.aeval_C] at key
  exact ‚ü®q.coeff 0, hq0, key‚ü©

end integrallemma

-- Charpoly of a finite group acting on a ring
section charpoly

open Polynomial

variable {B : Type*} [CommRing B] (G : Type*) [Group G] [Fintype G] [MulSemiringAction G B]

noncomputable def MulSemiringAction.charpoly (b : B) : B[X] :=
  ‚àè g : G, (X - C (g ‚Ä¢ b))

namespace MulSemiringAction.Charpoly

theorem charpoly_eq (b : B) : charpoly G b = ‚àè g : G, (X - C (g ‚Ä¢ b)) := rfl

theorem charpoly_eq_prod_smul (b : B) : charpoly G b = ‚àè g : G, g ‚Ä¢ (X - C b) := by
  simp only [smul_sub, smul_C, smul_X, charpoly_eq]

theorem monic_charpoly (b : B) : (charpoly G b).Monic :=
  monic_prod_of_monic _ _ (fun _ _ ‚Ü¶ monic_X_sub_C _)

theorem eval_charpoly (b : B) : (charpoly G b).eval b = 0 := by
  rw [charpoly_eq, eval_prod]
  apply Finset.prod_eq_zero (Finset.mem_univ (1 : G))
  rw [one_smul, eval_sub, eval_C, eval_X, sub_self]

variable {G}

theorem smul_charpoly (œÉ : G) (b : B) : œÉ ‚Ä¢ (charpoly G b) = charpoly G b := by
  rw [charpoly_eq_prod_smul, Finset.smul_prod_perm]

private theorem smul_coeff_charpoly (b : B) (n : ‚Ñï) (g : G) :
    g ‚Ä¢ (charpoly G b).coeff n = (charpoly G b).coeff n := by
  rw [‚Üê coeff_smul, smul_charpoly]

end MulSemiringAction.Charpoly

end charpoly

-- Charpoly of a finite group acting on an algebra extension
section charpoly

namespace MulSemiringAction.Charpoly

open Polynomial BigOperators

variable {A B : Type*} [CommRing A] [CommRing B] [Algebra A B]
  (G : Type*) [Group G] [Fintype G] [MulSemiringAction G B]

theorem reduction
    (hinv : ‚àÄ (b : B), (‚àÄ (g : G), g ‚Ä¢ b = b) ‚Üí ‚àÉ a : A, algebraMap A B a = b) (b : B) :
    ‚àÉ M : A[X], M.Monic ‚àß M.map (algebraMap A B) = charpoly G b := by
  let f : ‚Ñï ‚Üí A := fun k ‚Ü¶ (hinv ((charpoly G b).coeff k) (smul_coeff_charpoly b k)).choose
  have hf : ‚àÄ k, algebraMap A B (f k) = (charpoly G b).coeff k :=
    fun k ‚Ü¶ (hinv ((charpoly G b).coeff k) (smul_coeff_charpoly b k)).choose_spec
  use X ^ (charpoly G b).natDegree + ‚àë k ‚àà Finset.range (charpoly G b).natDegree, C (f k) * X ^ k
  constructor
  ¬∑ apply Polynomial.monic_X_pow_add
    rw [‚Üê Fin.sum_univ_eq_sum_range]
    apply Polynomial.degree_sum_fin_lt
  ¬∑ simp_rw [Polynomial.map_add, Polynomial.map_sum, Polynomial.map_mul, Polynomial.map_pow,
      Polynomial.map_X, Polynomial.map_C, hf]
    exact (monic_charpoly G b).as_sum.symm

variable (P : Ideal A) (Q : Ideal B) [Algebra (A ‚ß∏ P) (B ‚ß∏ Q)] [IsScalarTower A (A ‚ß∏ P) (B ‚ß∏ Q)]

theorem reduction_isIntegral
    (hFull' : ‚àÄ (b : B), (‚àÄ (g : G), g ‚Ä¢ b = b) ‚Üí ‚àÉ a : A, algebraMap A B a = b) :
    Algebra.IsIntegral (A ‚ß∏ P) (B ‚ß∏ Q) where
  isIntegral q := by
    obtain ‚ü®b, rfl‚ü© := Ideal.Quotient.mk_surjective q
    change IsIntegral (A ‚ß∏ P) (algebraMap B (B ‚ß∏ Q) b)
    obtain ‚ü®f, hf1, hf2‚ü© := reduction G hFull' b
    refine ‚ü®f.map (algebraMap A (A ‚ß∏ P)), hf1.map (algebraMap A (A ‚ß∏ P)), ?_‚ü©
    rw [‚Üê eval_map, map_map, ‚Üê IsScalarTower.algebraMap_eq,
        IsScalarTower.algebraMap_eq A B (B ‚ß∏ Q), ‚Üê map_map, hf2, eval_map, eval‚ÇÇ_at_apply,
        eval_charpoly, map_zero]

end MulSemiringAction.Charpoly

end charpoly

section stabilizerAction

variable {A B : Type*} [CommRing A] [CommRing B] [Algebra A B]
  (G : Type*) [Group G] [Finite G] [MulSemiringAction G B] [SMulCommClass G A B]
  (P : Ideal A) (Q : Ideal B) [Algebra (A ‚ß∏ P) (B ‚ß∏ Q)] [IsScalarTower A (A ‚ß∏ P) (B ‚ß∏ Q)]

def stabilizerAction : MulAction.stabilizer G Q ‚Üí* ((B ‚ß∏ Q) ‚âÉ‚Çê[A ‚ß∏ P] (B ‚ß∏ Q)) where
  toFun g :=
  { __ := Ideal.quotientEquiv Q Q (MulSemiringAction.toRingEquiv G B g) g.2.symm
    commutes' := fun q ‚Ü¶ by
      obtain ‚ü®a, rfl‚ü© := Ideal.Quotient.mk_surjective q
      simp [‚Üê Ideal.Quotient.algebraMap_eq, ‚Üê IsScalarTower.algebraMap_apply] }
  map_one' := AlgEquiv.ext (fun q ‚Ü¶ by
    obtain ‚ü®b, rfl‚ü© := Ideal.Quotient.mk_surjective q
    simp)
  map_mul' g h := AlgEquiv.ext (fun q ‚Ü¶ by
    obtain ‚ü®b, rfl‚ü© := Ideal.Quotient.mk_surjective q
    simp [mul_smul])

end stabilizerAction

section CRT

variable {B : Type*} [CommRing B] (G : Type*) [Group G] [Finite G] [MulSemiringAction G B]
  (Q : Ideal B) [Q.IsPrime]

-- technical CRT lemma
theorem lem1 [DecidableEq (Ideal B)] :
    ‚àÉ a b : B, (‚àÄ g : G, g ‚Ä¢ a = a) ‚àß (a ‚àâ Q) ‚àß
    (‚àÄ g : G, algebraMap B (B ‚ß∏ Q) (g ‚Ä¢ b) =
      algebraMap B (B ‚ß∏ Q) (if g ‚Ä¢ Q = Q then a else 0)) := by
  obtain ‚ü®_‚ü© := nonempty_fintype G
  let P := ((Finset.univ : Finset G).filter (fun g ‚Ü¶ g ‚Ä¢ Q ‚â† Q)).inf (fun g ‚Ü¶ g ‚Ä¢ Q)
  have h1 : ¬¨ P ‚â§ Q := by
    rw [Ideal.IsPrime.inf_le' inferInstance]
    rintro ‚ü®g, hg1, hg2‚ü©
    exact (Finset.mem_filter.mp hg1).2 (smul_eq_of_smul_le hg2)
  obtain ‚ü®b, hbP, hbQ‚ü© := SetLike.not_le_iff_exists.mp h1
  replace hbP : ‚àÄ g : G, g ‚Ä¢ Q ‚â† Q ‚Üí b ‚àà g ‚Ä¢ Q :=
    fun g hg ‚Ü¶ (Finset.inf_le (Finset.mem_filter.mpr ‚ü®Finset.mem_univ g, hg‚ü©) : P ‚â§ g ‚Ä¢ Q) hbP
  let f := MulSemiringAction.charpoly G b
  obtain ‚ü®q, hq, hq0‚ü© :=
    (f.map (algebraMap B (B ‚ß∏ Q))).exists_eq_pow_rootMultiplicity_mul_and_not_dvd
      (Polynomial.map_monic_ne_zero (MulSemiringAction.Charpoly.monic_charpoly G b)) 0
  rw [map_zero, sub_zero] at hq hq0
  let j := (f.map (algebraMap B (B ‚ß∏ Q))).rootMultiplicity 0
  let k := q.natDegree
  let r := ‚àë i ‚àà Finset.range (k + 1), Polynomial.monomial i (f.coeff (i + j))
  have hr : r.map (algebraMap B (B ‚ß∏ Q)) = q := by
    ext n
    rw [Polynomial.coeff_map, Polynomial.finset_sum_coeff]
    simp only [Polynomial.coeff_monomial, Finset.sum_ite_eq', Finset.mem_range_succ_iff]
    split_ifs with hn
    ¬∑ rw [‚Üê Polynomial.coeff_map, hq, Polynomial.coeff_X_pow_mul]
    ¬∑ rw [map_zero, eq_comm, Polynomial.coeff_eq_zero_of_natDegree_lt (lt_of_not_le hn)]
  have hf : f.eval b = 0 := MulSemiringAction.Charpoly.eval_charpoly G b
  have hr : r.eval b ‚àà Q := by
    rw [‚Üê Ideal.Quotient.eq_zero_iff_mem, ‚Üê Ideal.Quotient.algebraMap_eq] at hbQ ‚ä¢
    replace hf := congrArg (algebraMap B (B ‚ß∏ Q)) hf
    rw [‚Üê Polynomial.eval‚ÇÇ_at_apply, ‚Üê Polynomial.eval_map] at hf ‚ä¢
    rwa [map_zero, hq, ‚Üê hr, Polynomial.eval_mul, Polynomial.eval_pow, Polynomial.eval_X,
      mul_eq_zero, or_iff_right (pow_ne_zero _ hbQ)] at hf
  let a := f.coeff j
  have ha : ‚àÄ g : G, g ‚Ä¢ a = a := MulSemiringAction.Charpoly.smul_coeff_charpoly b j
  have hr' : ‚àÄ g : G, g ‚Ä¢ Q ‚â† Q ‚Üí a - r.eval b ‚àà g ‚Ä¢ Q := by
    intro g hg
    have hr : r = ‚àë i ‚àà Finset.range (k + 1), Polynomial.monomial i (f.coeff (i + j)) := rfl
    rw [‚Üê Ideal.neg_mem_iff, neg_sub, hr, Finset.sum_range_succ', Polynomial.eval_add,
        Polynomial.eval_monomial, zero_add, pow_zero, mul_one, add_sub_cancel_right]
    simp only [ ‚Üê Polynomial.monomial_mul_X]
    rw [‚Üê Finset.sum_mul, Polynomial.eval_mul_X]
    exact Ideal.mul_mem_left (g ‚Ä¢ Q) _ (hbP g hg)
  refine ‚ü®a, a - r.eval b, ha, ?_, fun h ‚Ü¶ ?_‚ü©
  ¬∑ rwa [‚Üê Ideal.Quotient.eq_zero_iff_mem, ‚Üê Ideal.Quotient.algebraMap_eq, ‚Üê Polynomial.coeff_map,
      ‚Üê zero_add j, hq, Polynomial.coeff_X_pow_mul, ‚Üê Polynomial.X_dvd_iff]
  ¬∑ rw [‚Üê sub_eq_zero, ‚Üê map_sub, Ideal.Quotient.algebraMap_eq, Ideal.Quotient.eq_zero_iff_mem,
      ‚Üê Ideal.smul_mem_pointwise_smul_iff (a := h‚Åª¬π), smul_sub, inv_smul_smul]
    simp only [‚Üê eq_inv_smul_iff (g := h), eq_comm (a := Q)]
    split_ifs with hh
    ¬∑ rwa [ha, sub_sub_cancel_left, hh, Q.neg_mem_iff]
    ¬∑ rw [smul_zero, sub_zero]
      exact hr' h‚Åª¬π hh

theorem lem2 [DecidableEq (Ideal B)] (b‚ÇÄ : B)
    (hx : ‚àÄ g : G, g ‚Ä¢ Q = Q ‚Üí algebraMap B (B ‚ß∏ Q) (g ‚Ä¢ b‚ÇÄ) = algebraMap B (B ‚ß∏ Q) b‚ÇÄ) :
    ‚àÉ a b : B, (‚àÄ g : G, g ‚Ä¢ a = a) ‚àß (a ‚àâ Q) ‚àß
    (‚àÄ g : G, algebraMap B (B ‚ß∏ Q) (g ‚Ä¢ b) =
      algebraMap B (B ‚ß∏ Q) (if g ‚Ä¢ Q = Q then a * b‚ÇÄ else 0)) := by
  obtain ‚ü®a, b, ha1, ha2, hb‚ü© := lem1 G Q
  refine ‚ü®a, b * b‚ÇÄ, ha1, ha2, fun g ‚Ü¶ ?_‚ü©
  rw [smul_mul', map_mul, hb]
  specialize hb g
  split_ifs with hg
  ¬∑ rw [map_mul, hx g hg]
  ¬∑ rw [map_zero, zero_mul]

end CRT

section polylemma

open Polynomial

theorem lem3 {R S : Type*} [CommRing R] [CommRing S] [Algebra R S] [NoZeroDivisors S]
    {a : S} {i j : ‚Ñï} {p : Polynomial R} (h : p.map (algebraMap R S) = (X - C a) ^ i * X ^ j)
    (f : S ‚âÉ‚Çê[R] S) (hi : i ‚â† 0) :
    f a = a := by
  by_cases ha : a = 0
  ¬∑ rw [ha, map_zero]
  have hf := congrArg (eval a) (congrArg (Polynomial.mapAlgHom f.toAlgHom) h)
  rw [coe_mapAlgHom, map_map, f.toAlgHom.comp_algebraMap, h] at hf
  simp_rw [Polynomial.map_mul, Polynomial.map_pow, Polynomial.map_sub, map_X, map_C,
    eval_mul, eval_pow, eval_sub, eval_X, eval_C, sub_self, zero_pow hi, zero_mul,
    zero_eq_mul, or_iff_left (pow_ne_zero j ha), pow_eq_zero_iff hi, sub_eq_zero] at hf
  exact hf.symm

end polylemma

section part_b

section redo_part_b

variable {A B : Type*} [CommRing A] [CommRing B] [Algebra A B]
  (G : Type*) [Group G] [Finite G] [MulSemiringAction G B] [SMulCommClass G A B]
  (P : Ideal A) (Q : Ideal B) [Q.IsPrime]
  [Algebra (A ‚ß∏ P) (B ‚ß∏ Q)] [IsScalarTower A (A ‚ß∏ P) (B ‚ß∏ Q)]
  variable (K L : Type*) [Field K] [Field L]
  [Algebra (A ‚ß∏ P) K]
  [Algebra (B ‚ß∏ Q) L] [NoZeroSMulDivisors (B ‚ß∏ Q) L]
  [Algebra (A ‚ß∏ P) L] [IsScalarTower (A ‚ß∏ P) (B ‚ß∏ Q) L]
  [Algebra K L] [IsScalarTower (A ‚ß∏ P) K L]

open IsScalarTower Polynomial in
theorem lem4 (hAB : ‚àÄ (b : B), (‚àÄ (g : G), g ‚Ä¢ b = b) ‚Üí ‚àÉ a : A, algebraMap A B a = b)
    (f : L ‚âÉ‚Çê[K] L) (b : B ‚ß∏ Q)
    (hx : ‚àÄ g : MulAction.stabilizer G Q, stabilizerAction G P Q g b = b) :
    f (algebraMap (B ‚ß∏ Q) L b) = (algebraMap (B ‚ß∏ Q) L b) := by
  classical
  cases nonempty_fintype G
  revert hx
  obtain ‚ü®b‚ÇÄ, rfl‚ü© := Ideal.Quotient.mk_surjective b
  intro hx
  rw [‚Üê Ideal.Quotient.algebraMap_eq]
  obtain ‚ü®a, b, ha1, ha2, hb‚ü© := lem2 G Q b‚ÇÄ (fun g hg ‚Ü¶ hx ‚ü®g, hg‚ü©)
  obtain ‚ü®M, _, key‚ü© := MulSemiringAction.Charpoly.reduction G hAB b
  replace key := congrArg (map (algebraMap B (B ‚ß∏ Q))) key
  rw [map_map, ‚Üê algebraMap_eq, algebraMap_eq A (A ‚ß∏ P) (B ‚ß∏ Q),
      ‚Üê map_map, MulSemiringAction.charpoly, Polynomial.map_prod] at key
  have key‚ÇÄ : ‚àÄ g : G, (X - C (g ‚Ä¢ b)).map (algebraMap B (B ‚ß∏ Q)) =
      if g ‚Ä¢ Q = Q then X - C (algebraMap B (B ‚ß∏ Q) (a * b‚ÇÄ)) else X := by
    intro g
    rw [Polynomial.map_sub, map_X, map_C, hb]
    split_ifs
    ¬∑ rfl
    ¬∑ rw [map_zero, map_zero, sub_zero]
  simp only [key‚ÇÄ, Finset.prod_ite, Finset.prod_const] at key
  replace key := congrArg (map (algebraMap (B ‚ß∏ Q) L)) key
  rw [map_map, ‚Üê algebraMap_eq, algebraMap_eq (A ‚ß∏ P) K L,
      ‚Üê map_map, Polynomial.map_mul, Polynomial.map_pow, Polynomial.map_pow, Polynomial.map_sub,
      map_X, map_C] at key
  replace key := lem3 key f (Finset.card_ne_zero_of_mem (Finset.mem_filter.mpr
    ‚ü®Finset.mem_univ 1, one_smul G Q‚ü©))
  simp only [map_mul] at key
  obtain ‚ü®a, rfl‚ü© := hAB a ha1
  rwa [‚Üê algebraMap_apply A B (B ‚ß∏ Q), algebraMap_apply A (A ‚ß∏ P) (B ‚ß∏ Q),
      ‚Üê algebraMap_apply, algebraMap_apply (A ‚ß∏ P) K L, f.commutes, mul_right_inj'] at key
  rwa [‚Üê algebraMap_apply, algebraMap_apply (A ‚ß∏ P) (B ‚ß∏ Q) L,
      ‚Üê algebraMap_apply A (A ‚ß∏ P) (B ‚ß∏ Q), algebraMap_apply A B (B ‚ß∏ Q),
      Ne, NoZeroSMulDivisors.algebraMap_eq_zero_iff, Ideal.Quotient.algebraMap_eq,
      Ideal.Quotient.eq_zero_iff_mem]

end redo_part_b

variable {A B : Type*} [CommRing A] [CommRing B] [Algebra A B]
  (G : Type*) [Group G] [Finite G] [MulSemiringAction G B] [SMulCommClass G A B]
  (P : Ideal A) (Q : Ideal B) [P.IsPrime] [Q.IsPrime]
  [Algebra (A ‚ß∏ P) (B ‚ß∏ Q)] [IsScalarTower A (A ‚ß∏ P) (B ‚ß∏ Q)]
  variable (K L : Type*) [Field K] [Field L]
  [Algebra (A ‚ß∏ P) K] [IsFractionRing (A ‚ß∏ P) K]
  [Algebra (B ‚ß∏ Q) L] [IsFractionRing (B ‚ß∏ Q) L]
  [Algebra (A ‚ß∏ P) L] [IsScalarTower (A ‚ß∏ P) (B ‚ß∏ Q) L]
  [Algebra K L] [IsScalarTower (A ‚ß∏ P) K L]

noncomputable def fullHom : MulAction.stabilizer G Q ‚Üí* (L ‚âÉ‚Çê[K] L) :=
  MonoidHom.comp (liftHom (A ‚ß∏ P) (B ‚ß∏ Q) K L) (stabilizerAction G P Q)

theorem fullHom_surjective1
    (hAB : ‚àÄ (b : B), (‚àÄ (g : G), g ‚Ä¢ b = b) ‚Üí ‚àÉ a : A, algebraMap A B a = b)
    (f : L ‚âÉ‚Çê[K] L) (x : L) (hx : ‚àÄ g : MulAction.stabilizer G Q, fullHom G P Q K L g x = x) :
    f x = x := by
  obtain ‚ü®_‚ü© := nonempty_fintype G
  have : NoZeroSMulDivisors (A ‚ß∏ P) L := by
    simp only [NoZeroSMulDivisors.iff_algebraMap_injective,
        injective_iff_map_eq_zero,
        IsScalarTower.algebraMap_eq (A ‚ß∏ P) K L,
        RingHom.comp_apply,
        NoZeroSMulDivisors.algebraMap_eq_zero_iff]
    simp
  have : NoZeroSMulDivisors (A ‚ß∏ P) (B ‚ß∏ Q) := by
    rw [NoZeroSMulDivisors.iff_algebraMap_injective] at this ‚ä¢
    rw [IsScalarTower.algebraMap_eq (A ‚ß∏ P) (B ‚ß∏ Q) L] at this
    exact Function.Injective.of_comp this
  have key : ‚àÉ (a : A ‚ß∏ P) (b : B ‚ß∏ Q), a ‚â† 0 ‚àß
      x = (algebraMap (B ‚ß∏ Q) L b) / (algebraMap (A ‚ß∏ P) L a) := by
    obtain ‚ü®x, y, hy, rfl‚ü© := IsFractionRing.div_surjective (A := B ‚ß∏ Q) x
    replace hy : y ‚â† 0 := by
      rintro rfl
      exact zero_not_mem_nonZeroDivisors hy
    have : Algebra.IsIntegral (A ‚ß∏ P) (B ‚ß∏ Q) :=
      MulSemiringAction.Charpoly.reduction_isIntegral G P Q hAB
    obtain ‚ü®a, ha, b, hb‚ü© := Algebra.exists_dvd_nonzero_if_isIntegral (A ‚ß∏ P) (B ‚ß∏ Q) y hy
    refine ‚ü®a, x * b, ha, ?_‚ü©
    rw [IsScalarTower.algebraMap_apply (A ‚ß∏ P) (B ‚ß∏ Q) L, hb]
    simp only [map_mul]
    rw [mul_div_mul_right]
    rw [Ne, NoZeroSMulDivisors.algebraMap_eq_zero_iff]
    rintro rfl
    rw [mul_zero] at hb
    rw [NoZeroSMulDivisors.algebraMap_eq_zero_iff] at hb
    exact ha hb
  obtain ‚ü®a, b, ha, rfl‚ü© := key
  simp only [map_div‚ÇÄ, IsScalarTower.algebraMap_apply (A ‚ß∏ P) K L,
    AlgEquiv.commutes] at hx ‚ä¢
  have key : algebraMap (A ‚ß∏ P) L a ‚â† 0 := by
    rwa [Ne, NoZeroSMulDivisors.algebraMap_eq_zero_iff]
  simp only [‚Üê IsScalarTower.algebraMap_apply (A ‚ß∏ P) K L] at hx ‚ä¢
  simp only [div_left_inj' key] at hx ‚ä¢
  refine lem4 G P Q K L hAB f b ?_
  intro g
  specialize hx g
  apply IsFractionRing.injective (B ‚ß∏ Q) L
  rw [fullHom] at hx
  simp only [MonoidHom.coe_comp, Function.comp_apply] at hx
  rw [‚Üê hx]
  symm
  apply liftHom_commutes

theorem fullHom_surjective
    (hAB : ‚àÄ (b : B), (‚àÄ (g : G), g ‚Ä¢ b = b) ‚Üí ‚àÉ a : A, algebraMap A B a = b) :
    Function.Surjective (fullHom G P Q K L : MulAction.stabilizer G Q ‚Üí* (L ‚âÉ‚Çê[K] L)) := by
  let action : MulSemiringAction (MulAction.stabilizer G Q) L :=
    MulSemiringAction.ofAlgEquivHom _ _
      (fullHom G P Q K L : MulAction.stabilizer G Q ‚Üí* (L ‚âÉ‚Çê[K] L))
  have key := toAlgHom_surjective (MulAction.stabilizer G Q) L
  intro f
  obtain ‚ü®g, hg‚ü© := key (AlgEquiv.ofRingEquiv (f := f)
    (fun x ‚Ü¶ fullHom_surjective1 G P Q K L hAB f x x.2))
  exact ‚ü®g, by rwa [AlgEquiv.ext_iff] at hg ‚ä¢‚ü©

end part_b

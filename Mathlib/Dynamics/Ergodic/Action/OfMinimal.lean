/-
Copyright (c) 2024 Yury Kudryashov. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Yury Kudryashov
-/
import Mathlib.Dynamics.Ergodic.Action.Regular
import Mathlib.MeasureTheory.Measure.ContinuousPreimage
import Mathlib.MeasureTheory.Measure.Haar.Unique

/-!
# Ergodicity from minimality


-/

open MeasureTheory Filter Set Function
open scoped Pointwise Topology

variable {X : Type*} [TopologicalSpace X] [R1Space X] [MeasurableSpace X] [BorelSpace X]

@[to_additive]
theorem aeconst_of_dense_setOf_preimage_smul_ae (G : Type*) [SMul G X]
    [TopologicalSpace G] [ContinuousSMul G X]
    {Œº : Measure X} [IsFiniteMeasure Œº] [Œº.InnerRegular] [ErgodicSMul G X Œº]
    {s : Set X} (hsm : NullMeasurableSet s Œº)
    (hd : Dense {g : G | (g ‚Ä¢ ¬∑) ‚Åª¬π' s =·µê[Œº] s}) : EventuallyConst s (ae Œº) := by
  borelize G
  refine aeconst_of_forall_preimage_smul_ae_eq G hsm ?_
  rwa [dense_iff_closure_eq, IsClosed.closure_eq, eq_univ_iff_forall] at hd
  let f : C(G √ó X, X) := ‚ü®(¬∑ ‚Ä¢ ¬∑).uncurry, continuous_smul‚ü©
  exact isClosed_setOf_preimage_ae_eq f.curry.continuous (measurePreserving_smul ¬∑ Œº) _ hsm
    (measure_ne_top _ _)

@[to_additive]
theorem aeconst_of_dense_setOf_preimage_smul_eq (G : Type*) [SMul G X]
    [TopologicalSpace G] [ContinuousSMul G X]
    {Œº : Measure X} [IsFiniteMeasure Œº] [Œº.InnerRegular] [ErgodicSMul G X Œº]
    {s : Set X} (hsm : NullMeasurableSet s Œº)
    (hd : Dense {g : G | (g ‚Ä¢ ¬∑) ‚Åª¬π' s = s}) : EventuallyConst s (ae Œº) :=
  aeconst_of_dense_setOf_preimage_smul_ae G hsm <| hd.mono fun _ h ‚Ü¶ mem_setOf.2 <| .of_eq h

@[to_additive]
theorem aeconst_of_dense_aestabilizer_smul {G : Type*} [Group G] [MulAction G X]
    [TopologicalSpace G] [ContinuousSMul G X] [ContinuousInv G]
    {Œº : Measure X} [IsFiniteMeasure Œº] [Œº.InnerRegular] [ErgodicSMul G X Œº]
    {s : Set X} (hsm : NullMeasurableSet s Œº)
    (hd : Dense (MulAction.aestabilizer G Œº s : Set G)) : EventuallyConst s (ae Œº) :=
  aeconst_of_dense_setOf_preimage_smul_ae G hsm <| (hd.preimage (isOpenMap_inv _)).mono <|
    fun g hg ‚Ü¶ by simpa only [preimage_smul] using hg

@[to_additive]
theorem ErgodicSMul.trans_isMinimal (M N : Type*) [Monoid M] [MulAction M N]
    [Monoid N] [TopologicalSpace N] [MulAction.IsMinimal M N]
    [MulAction N X] [SMul M X] [IsScalarTower M N X]
    (Œº : Measure X) [IsFiniteMeasure Œº] [Œº.InnerRegular] [ContinuousSMul N X] [ErgodicSMul N X Œº] :
    ErgodicSMul M X Œº where
  measure_preimage_smul c s hsm := by
    simpa only [smul_one_smul] using SMulInvariantMeasure.measure_preimage_smul (c ‚Ä¢ 1 : N) hsm
  aeconst_of_forall_preimage_smul_ae_eq {s} hsm hs := by
    refine aeconst_of_dense_setOf_preimage_smul_ae N hsm.nullMeasurableSet ?_
    refine (MulAction.dense_orbit M 1).mono ?_
    rintro _ ‚ü®g, rfl‚ü©
    simpa using hs g

@[to_additive]
theorem ergodic_smul_of_denseRange_pow {M : Type*} [Monoid M] [TopologicalSpace M]
    [MulAction M X] [ContinuousSMul M X] {g : M} (hg : DenseRange (g ^ ¬∑ : ‚Ñï ‚Üí M))
    (Œº : Measure X) [IsFiniteMeasure Œº] [Œº.InnerRegular] [ErgodicSMul M X Œº] :
    Ergodic (g ‚Ä¢ ¬∑) Œº := by
  borelize M
  refine ‚ü®measurePreserving_smul _ _, ‚ü®fun s hsm hs ‚Ü¶ ?_‚ü©‚ü©
  refine aeconst_of_dense_setOf_preimage_smul_eq M hsm.nullMeasurableSet (hg.mono ?_)
  refine range_subset_iff.2 fun n ‚Ü¶ ?_
  rw [mem_setOf, ‚Üê smul_iterate, preimage_iterate_eq, iterate_fixed hs]

@[to_additive]
theorem ergodic_smul_of_denseRange_zpow {G : Type*} [Group G] [TopologicalSpace G]
    [ContinuousInv G] [MulAction G X] [ContinuousSMul G X] {g : G} (hg : DenseRange (g ^ ¬∑ : ‚Ñ§ ‚Üí G))
    (Œº : Measure X) [IsFiniteMeasure Œº] [Œº.InnerRegular] [ErgodicSMul G X Œº] :
    Ergodic (g ‚Ä¢ ¬∑) Œº := by
  borelize G
  refine ‚ü®measurePreserving_smul _ _, ‚ü®fun s hsm hs ‚Ü¶ ?_‚ü©‚ü©
  refine aeconst_of_dense_aestabilizer_smul hsm.nullMeasurableSet (hg.mono ?_)
  rw [‚Üê Subgroup.coe_zpowers, SetLike.coe_subset_coe, ‚Üê Subgroup.zpowers_inv, Subgroup.zpowers_le,
    MulAction.mem_aestabilizer, ‚Üê preimage_smul, hs]

@[to_additive]
theorem ergodic_mul_left_of_denseRange_pow {G : Type*} [Group G]
    [TopologicalSpace G] [TopologicalGroup G] [SecondCountableTopology G]
    [MeasurableSpace G] [BorelSpace G]
    {g : G} (hg : DenseRange (g ^ ¬∑ : ‚Ñï ‚Üí G))
    (Œº : Measure G) [IsFiniteMeasure Œº] [Œº.InnerRegular] [Œº.IsMulLeftInvariant] :
    Ergodic (g * ¬∑) Œº :=
  ergodic_smul_of_denseRange_pow hg Œº

@[to_additive]
theorem ergodic_mul_left_of_denseRange_zpow {G : Type*} [Group G]
    [TopologicalSpace G] [TopologicalGroup G] [SecondCountableTopology G]
    [MeasurableSpace G] [BorelSpace G]
    {g : G} (hg : DenseRange (g ^ ¬∑ : ‚Ñ§ ‚Üí G))
    (Œº : Measure G) [IsFiniteMeasure Œº] [Œº.InnerRegular] [Œº.IsMulLeftInvariant] :
    Ergodic (g * ¬∑) Œº :=
  ergodic_smul_of_denseRange_zpow hg Œº

@[to_additive]
theorem DenseRange.zpow_of_ergodic_mul_left {G : Type*} [Group G] [TopologicalSpace G]
    [TopologicalGroup G] [MeasurableSpace G] [OpensMeasurableSpace G]
    {Œº : Measure G} [Œº.IsOpenPosMeasure] [Œº.IsMulLeftInvariant] {g : G} (hg : Ergodic (g * ¬∑) Œº) :
    DenseRange (g ^ ¬∑ : ‚Ñ§ ‚Üí G) := by
  intro a
  by_contra h
  obtain ‚ü®V, hV‚ÇÅ, hVo, hV‚ü© :
      ‚àÉ V : Set G, 1 ‚àà V ‚àß IsOpen V ‚àß ‚àÄ x ‚àà V, ‚àÄ y ‚àà V, ‚àÄ m : ‚Ñ§, g ^ m ‚â† a * x / y := by
    rw [‚Üê mem_compl_iff, ‚Üê interior_compl, mem_interior_iff_mem_nhds] at h
    have : Tendsto (fun (x, y) ‚Ü¶ a * x / y) (ùìù 1) (ùìù a) :=
      Continuous.tendsto' (by fun_prop) _ _ (by simp)
    rw [nhds_prod_eq] at this
    simpa [(nhds_basis_opens (1 : G)).prod_self.mem_iff, prod_subset_iff, and_assoc] using this h
  set s := ‚ãÉ m : ‚Ñ§, g ^ m ‚Ä¢ V
  have hso : IsOpen s := isOpen_iUnion fun m ‚Ü¶ hVo.smul _
  have hsne : s.Nonempty := ‚ü®1, mem_iUnion.2 ‚ü®0, by simpa‚ü©‚ü©
  have hd : Disjoint s (a ‚Ä¢ V) := by
    simp_rw [s, disjoint_iUnion_left, disjoint_left]
    rintro m _ ‚ü®x, hx, rfl‚ü© ‚ü®y, hy, hxy‚ü©
    apply hV y hy x hx m
    simp_all
  have hgs : (g * ¬∑) ‚Åª¬π' s = s := by
    simp only [s, preimage_iUnion, ‚Üê smul_eq_mul, preimage_smul]
    refine iUnion_congr_of_surjective _ (add_left_surjective (-1)) fun m ‚Ü¶ ?_
    simp [zpow_add, mul_smul]
  cases hg.measure_self_or_compl_eq_zero hso.measurableSet hgs with
  | inl h => exact hso.measure_ne_zero _ hsne h
  | inr h =>
    refine (hVo.smul a).measure_ne_zero Œº (.image _ ‚ü®1, hV‚ÇÅ‚ü©) (measure_mono_null ?_ h)
    rwa [disjoint_right] at hd

@[to_additive]
theorem ergodic_mul_left_iff_denseRange_zpow {G : Type*} [Group G]
    [TopologicalSpace G] [TopologicalGroup G] [SecondCountableTopology G]
    [MeasurableSpace G] [BorelSpace G]
    {g : G} (Œº : Measure G) [IsFiniteMeasure Œº] [Œº.InnerRegular] [Œº.IsMulLeftInvariant]
    [Œº.IsOpenPosMeasure] : Ergodic (g * ¬∑) Œº ‚Üî DenseRange (g ^ ¬∑ : ‚Ñ§ ‚Üí G) :=
  ‚ü®.zpow_of_ergodic_mul_left, (ergodic_mul_left_of_denseRange_zpow ¬∑ Œº)‚ü©

@[to_additive]
theorem MonoidHom.preErgodic_of_dense_iUnion_preimage_one {G : Type*} [Group G] [TopologicalSpace G]
    [TopologicalGroup G] [SecondCountableTopology G] [MeasurableSpace G] [BorelSpace G]
    {Œº : Measure G} [IsFiniteMeasure Œº] [Œº.InnerRegular] [Œº.IsMulLeftInvariant]
    (f : G ‚Üí* G) (hf : Dense (‚ãÉ n, f^[n] ‚Åª¬π' 1)) : PreErgodic f Œº := by
  refine ‚ü®fun s hsm hs ‚Ü¶ aeconst_of_dense_setOf_preimage_smul_eq (G := G) hsm.nullMeasurableSet ?_‚ü©
  refine hf.mono <| iUnion_subset fun n x hx ‚Ü¶ ?_
  have hsn : f^[n] ‚Åª¬π' s = s := by
    rw [preimage_iterate_eq, iterate_fixed hs]
  rw [mem_preimage, Set.mem_one] at hx
  rw [mem_setOf, ‚Üê hsn]
  ext y
  simp [hx]

@[to_additive]
theorem MonoidHom.ergodic_of_dense_iUnion_preimage_one {G : Type*} [Group G] [TopologicalSpace G]
    [TopologicalGroup G] [CompactSpace G] [SecondCountableTopology G]
    [MeasurableSpace G] [BorelSpace G] {Œº : Measure G} [Œº.IsHaarMeasure]
    (f : G ‚Üí* G) (hf : Dense (‚ãÉ n, f^[n] ‚Åª¬π' 1)) (hcont : Continuous f) (hsurj : Surjective f) :
    Ergodic f Œº :=
  ‚ü®f.measurePreserving hcont hsurj rfl, f.preErgodic_of_dense_iUnion_preimage_one hf‚ü©
